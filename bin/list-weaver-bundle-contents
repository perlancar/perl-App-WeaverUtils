#!perl

# DATE
# VERSION

use 5.010;
use strict;
use warnings;

use App::WeaverUtils;
use Perinci::CmdLine::Any -prefer_lite=>1;

our %SPEC;
$SPEC{list_weaver_bundle_contents} = {
    v => 1.1,
    summary => 'List contents of a Pod::Weaver plugin bundle',
    args => {
        bundle => {
            schema => 'str*',
            req    => 1,
            pos    => 0,
            completion => $App::WeaverUtils::_complete_bundle,
        },
        short => {
            schema => 'bool*',
            cmdline_aliases => {s=>{}},
        },
    },
};
sub list_weaver_bundle_contents {
    require Module::Load;

    my %args = @_;

    my $bundle = $args{bundle};
    $bundle =~ s/^Pod::Weaver::PluginBundle:://;
    $bundle =~ s!/!::!g; # convenience

    my $module = "Pod::Weaver::PluginBundle::$bundle";
    Module::Load::load($module);

    my @cfg = $module->mvp_bundle_config;
    my $res;
    for (@cfg) {
        if ($args{short}) {
            $_->[1] =~ s/^Pod::Weaver:://;
        }
        push @$res, [$_->[0], $_->[1]];
    }

    [200, "OK", $res];
}

Perinci::CmdLine::Any->new(
    url => '/main/list_weaver_bundle_contents',
)->run;

# ABSTRACT: List contents of a Pod::Weaver plugin bundle
# PODNAME:

=head1 SYNOPSIS

 % list-weaver-bundle-contents Author::PERLANCAR

=cut
